apply plugin: 'maven-publish'
apply plugin: 'android-library'

version = "3.5.1"
group = "org.apache"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-codec:commons-codec:1.9'
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }

    defaultConfig {
//        minSdkVersion 7
//        targetSdkVersion 17
//        versionCode 1
//        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            // pull the transitive deps in
            // borrowed from: https://github.com/danieltwagner/android-async-logger/blob/master/build.gradle
            pom.withXml {
                def dependenciesRoot = asNode().appendNode('dependencies')
                // read the compile dependencies...
                def compileConfiguration = project.configurations.getByName("compile")
                def resolvedConfiguration = compileConfiguration.resolvedConfiguration
                def resolvedDependencies = resolvedConfiguration.firstLevelModuleDependencies
                resolvedDependencies.each { dp ->
                    // ... and generate the XML for each dependency manually
                    def thisDependency = dependenciesRoot.appendNode('dependency')
                    thisDependency.appendNode('groupId', dp.moduleGroup)
                    thisDependency.appendNode('artifactId', dp.moduleName)
                    thisDependency.appendNode('version', dp.moduleVersion)
                }
                return dependenciesRoot
            }
            artifact "build/outputs/aar/${project.name}-${project.version}.aar"
        }
    }
    repositories {
        maven {
            url "../../maven-decentral.github.io/m2/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
        }
    }
}
